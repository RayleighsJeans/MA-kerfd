## USE FIRST TIMESTEP In 2D HERE?
## time2d='00010120';

## hubbe is x, seti is y
if (tempzdiag == 1 && velzdiag == 1) {
  seti = 3;
	if (negion == 1) {
		seti = 4;
	}
	if (ntrlzconst == 0) {
		seti = 4;
		if (negion == 1) {
			seti = 5;
		}
	}
  hubbel = 6;
}
if (tempzdiag == 0 && velzdiag == 1) {
	seti = 3;
	if (negion == 1) {
		seti = 4;
	}
	if (ntrlzconst == 0) {
		seti = 4;
		if (negion == 1) {
			seti = 5;
		}
	}
	hubbel = 4;
}
if (tempzdiag == 1 && velzdiag == 0) {
	seti = 3;
	if (negion == 1) {
		seti = 4;
	}
	if (ntrlzconst == 0) {
		seti = 4;
		if (negion == 1) {
			seti = 5;
		}
	}
	hubbel = 3;
	if (debyediag == 1) {
		hubbel = 4;
	}
}
if (tempzdiag == 0 && velzdiag == 0) {
	seti = 2;
	hubbel = 2;
	if (negion == 1) {
		hubbel = 3;
	}
	if (ntrlzconst == 0) {
		hubbel = 3;
		if (negion == 1) {
			hubbel = 3;
		}
	}
	if (debyediag == 1) {
		hubbel = 3;
		if (negion == 1) {
			if (ntrlzconst == 1) {
			hubbel = 4;
		}
		}
	}
}

## size and time
xeich=1280
yeich=1280
x=hubbel*xeich;
y=seti*yeich;

## input parameters
set terminal pngcairo size x,y enhanced font 'PT Serif,21';
set output 'figs/'.twodrunid.'.allfigs.'.time2d.'.png';
set grid; unset key; set border linewidth 1.0; set tics nomirror out scale 0.5;
set xlabel 'z (cell)';
set ylabel 'r (cell)';
set xrange [0.0:sizez2d]; set yrange [0.0:nr]; set multiplot layout seti,hubbel;
load 'jet.pal';

#####################
## ALL PLOTS PHYSFIGS
print ">> ELECTRONS <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<";
set logscale cb; set cbrange [1e-5:1e1];
set format cb "10^{%T}"
set title "electron density at ".time2d
p '../out/e_dens_'.time2d.'.dat' matrix with image 
if (tempzdiag == 1) {
	set cbrange [*:*];
	set title "electron temperature at ".time2d
	p '../out/e_temp_'.time2d.'.dat' matrix with image
	set title "electron thermal temperature ".time2d
	p '../out/e_temp_therm_'.time2d.'.dat' matrix with image
}
if (velzdiag == 1) {
	unset logscale cb; set cbrange [*:*];
	set format cb "%g"
	set title "electron z velocity at ".time2d
	p '../out/e_velz_'.time2d.'.dat' matrix with image
	set title "electron r velocity at ".time2d
	p '../out/e_velr_'.time2d.'.dat' matrix with image 
	set title "electron thermal velocity at ".time2d
	p '../out/e_velt_'.time2d.'.dat' matrix with image
}

print ">> IONS <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<";
set logscale cb; set cbrange [1e-5:1e1];
set format cb "10^{%T}"
set title "ion density at ".time2d
p '../out/i_dens_'.time2d.'.dat' matrix with image 
if (tempzdiag == 1) {
	set cbrange [*:*];
	set title "ion temperature at ".time2d
	p '../out/i_temp_'.time2d.'.dat' matrix with image 
	set title "ion thermal temperature ".time2d
	p '../out/i_temp_therm_'.time2d.'.dat' matrix with image
}
if (velzdiag == 1) {
	unset logscale cb; set cbrange [*:*];
	set format cb "%g"
	set title "ion z velocity at ".time2d
	p '../out/i_velz_'.time2d.'.dat' matrix with image 
	set title "ion r velocity at ".time2d
	p '../out/i_velr_'.time2d.'.dat' matrix with image 
	set title "ion thermal velocity at ".time2d
	p '../out/i_velt_'.time2d.'.dat' matrix with image
}
if (negion == 1) {

print ">> NIONS <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<";
	set logscale cb; set cbrange [1e-5:1e2];
	set format cb "10^{%T}"
	set title "nion density at ".time2d
	p '../out/ni_dens_'.time2d.'.dat' matrix with image 
	if (tempzdiag == 1) {
		set cbrange [1e-20:1e5];
		set title "nion temperature at ".time2d
		p '../out/ni_temp_'.time2d.'.dat' matrix with image 
		set title "nion thermal temperature ".time2d
		p '../out/ni_temp_therm_'.time2d.'.dat' matrix with image
	}
	if (velzdiag == 1) {
		unset logscale cb;
		set cbrange [-2000:1000];
		set format cb "%g"
		set title "nion z velocity at ".time2d
		p '../out/ni_velz_'.time2d.'.dat' matrix with image 
		set title "nion r velocity at ".time2d
		p '../out/ni_velr_'.time2d.'.dat' matrix with image 
		set title "nion thermal velocity at ".time2d
		p '../out/ni_velt_'.time2d.'.dat' matrix with image
	}
}
if (ntrlzconst == 0) {

print ">> NEUTRALS <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<";
	set logscale cb; set cbrange [1e-5:1e2];
	set format cb "10^{%T}"
	set title "neutral density at ".time2d
	p '../out/n_dens_'.time2d.'.dat' matrix with image 
	if (tempzdiag == 1) {
		set cbrange [1e-20:1e5];
		set title "neutral temperature at ".time2d
		p '../out/n_temp_'.time2d.'.dat' matrix with image 
		set title "neutral thermal temperature ".time2d
		p '../out/n_temp_therm_'.time2d.'.dat' matrix with image
	}
	if (velzdiag == 1) {
		unset logscale cb;
		set cbrange [-2000:1000];
		set format cb "%g"
		set title "neutral z velocity at ".time2d
		p '../out/n_velz_'.time2d.'.dat' matrix with image 
		set title "neutral r velocity at ".time2d
		p '../out/n_velr_'.time2d.'.dat' matrix with image 
		set title "neutral thermal velocity at ".time2d
		p '../out/n_velt_'.time2d.'.dat' matrix with image
	}
}
if (debyediag == 1) {

print ">> DEBYE DIAG <<<<<<<<<<<<<<<<<<<<<<<<<<<<<";
	set logscale cb; set cbrange [*:*];
	set format cb "10^{%T}"
	set title "local debye length from temp. (total energy) at ".time2d
	p '../out/e_debye_'.time2d.'.dat' matrix with image
} 

print ">> POTENTIAL <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<";
unset logscale cb; set cbrange [*:*];
set format cb "%g"
set title "potential at ".time2d
p '../out/phi'.time2d.'.dat' matrix with image
unset xrange; unset yrange; 

print ">> TRACEPLOT <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<";
last2d=last2d+0;
set xrange [start2d:last2d]; set yrange [0.0:*];
set xlabel "time2d (#)"; set format x "%1.1l*10^{%T}";
set ylabel "trace counts (#)"
set title "particle counts"
set key outside
p '../trace.dat' u 2:3 with lines ls 1 title 'electrons', \
	'../trace.dat' u 2:4 with lines ls 4 title 'pos. ions', \
	'../trace.dat' u 2:5 with lines ls 7 title 'neg. ions', \
	'../trace.dat' u 2:6 with lines ls 3 title 'neutrals'

unset multiplot

if (femsolver == 1) {
  # FEM SOLVER 2D
  ## input parameters
  set terminal pngcairo size 3072,2048 enhanced font 'PT Serif,21';
  set grid; unset key; set border linewidth 1.0; set tics nomirror out scale 0.5;
  set xlabel 'z (cell)'; set ylabel 'r (cell)';
  set xrange [0.0:sizez2d]; set yrange [0.0:nr];
  load 'jet.pal';
  
  print ">> FEM DIAG 2D RELAXED <<<<<<<<<<<<<<<<<<<<";
  set output 'figs/'.twodrunid.'.fem_figs-relaxed.png';
  set multiplot layout 2,3; timecode='00000001';
  ## FIRST LINE
  # 1
  set title 'area weighted charge '.timecode; 
  plot '../out/aw_charge_'.timecode.'.dat' matrix with image;
  # 2
  set title 'cell face electric field relax'; 
  plot '../out/fem_efield_relax.dat' matrix with image;
  # 3
  set title 'old electric field relax'; 
  plot '../out/old_field_relax.dat' matrix with image;
  ## SECOND LINE
  # 4
  set title 'cell face current '.timecode; 
  plot '../out/cell_current_'.timecode.'.dat' matrix with image;
  # 5
  set title 'standard electric field relax'; 
  plot '../out/stand_efield_relax.dat' matrix with image;
  # 6
  set title 'difference in electric field relax'; 
  plot '../out/diff_fields_relax.dat' matrix with image;
  unset multiplot;
  reset;
  
  set terminal pngcairo size 3072,2048 enhanced font 'PT Serif,21';
  set grid; unset key; set border linewidth 1.0; set tics nomirror out scale 0.5;
  set xlabel 'z (cell)'; set ylabel 'r (cell)';
  set xrange [0.0:sizez2d]; set yrange [0.0:nr];
  load 'jet.pal';
  
  print ">> FEM DIAG 2D <<<<<<<<<<<<<<<<<<<<<<<<<<<<";
  set output 'figs/'.twodrunid.'.fem_figs.'.time2d.'.png';
  set multiplot layout 2,3;
  ## FIRST LINE
  # 1
  set title 'area weighted charge '.time2d; 
  plot '../out/aw_charge_'.time2d.'.dat' matrix with image;
  # 2
  set title 'cell face electric field relax'; 
  plot '../out/fem_efield_'.time2d.'.dat' matrix with image;
  # 3
  set title 'old electric field '.time2d; 
  plot '../out/old_field_'.time2d.'.dat' matrix with image;
  ## SECOND LINE
  # 4
  set title 'cell face current '.time2d; 
  plot '../out/cell_current_'.time2d.'.dat' matrix with image;
  # 5
  set title 'standard electric field '.time2d; 
  plot '../out/stand_efield_'.time2d.'.dat' matrix with image;
  # 6
  set title 'difference in electric field '.time2d; 
  plot '../out/diff_fields_'.time2d.'.dat' matrix with image;
  unset multiplot;
  reset;

  print ">> FEM ARROWS 2D RELAX <<<<<<<<<<<<<<<<<<<<";
  set terminal pngcairo enhanced size 2048,1024 font 'PT Serif,21';
  set grid; unset key; set border linewidth 1.0; set tics nomirror out scale 0.5;
  set xlabel 'z (cell)'; set ylabel 'r (cell)';
  set xrange [0.0-(sizez2d/10):sizez2d+(sizez2d/10)]; set yrange [0.0-(nr/10):nr+(nr/10)];
  load 'jet.pal';
  
  set xlabel 'z/cells'; set ylabel 'r/cells'
  set output 'figs/vector_fem_'.twodrunid.'_relax.png'
  
  set multiplot layout 1,2;
  set title 'fem electric field relax';
  plot '../out/vector_femfield_relax.dat' every 7::0 \
       u ($1):($2):($3*scale_arrow):($4*scale_arrow):($5) with vectors \
       head filled lw 2.0 lc palette;
  set title 'standard electric field relax';
  plot '../out/vector_stdfield_relax.dat' every 7::0 \
       u ($1):($2):($3*10):($4*10):($5) with vectors \
       head filled lw 2.0 lc palette;
  unset multiplot;
  reset;

  print ">> FEM ARROWS 2D <<<<<<<<<<<<<<<<<<<<<<<<<<";
  set terminal pngcairo enhanced size 2048,1024 font 'PT Serif,21';
  set grid; unset key; set border linewidth 1.0; set tics nomirror out scale 0.5;
  set xlabel 'z (cell)'; set ylabel 'r (cell)';
  set xrange [0.0-(sizez2d/10):sizez2d+(sizez2d/10)]; set yrange [0.0-(nr/10):nr+(nr/10)];
  load 'jet.pal';
  
  set xlabel 'z/cells'; set ylabel 'r/cells'
  set output 'figs/vector_fem_'.twodrunid.'_'.time2d.'.png'
  
  set multiplot layout 1,2;
  set title 'fem electric field '.time2d;
  plot '../out/vector_femfield_'.time2d.'.dat' every 7::0 \
       u ($1):($2):($3*scale_arrow):($4*scale_arrow):($5) with vectors \
       head filled lw 2.0 lc palette;
  set title 'standard electric field '.time2d;
  plot '../out/vector_stdfield_'.time2d.'.dat' every 7::0 \
       u ($1):($2):($3*scale_arrow):($4*scale_arrow):($5) with vectors \
       head filled lw 2.0 lc palette;
  unset multiplot;
  reset;
}

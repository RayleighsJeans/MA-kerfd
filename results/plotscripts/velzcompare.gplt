## input parameters
set terminal pngcairo enhanced size 3840,2160 font 'PT Serif,24'
set termoption dash
set samples samplesiso
set isosamples samplesiso

# LABELS AND BORDERS/TICKS
# COLORS AND DASHES 
set grid
set key outside
set border linewidth 3.0
set tics nomirror out scale 0.7
unset colorbox

# define line styles using explicit rgbcolor names
set style line 1 lt 1 lc rgb "black" lw 2
set style line 2 lt 2 lc rgb "black" lw 2
set style line 3 lt 5 lc rgb "black" lw 2
set style line 4 lt 1 lc rgb "red" lw 2
set style line 5 lt 2 lc rgb "red" lw 2
set style line 6 lt 5 lc rgb "red" lw 2
set style line 7 lt 1 lc rgb "blue" lw 2
set style line 8 lt 2 lc rgb "blue" lw 2
set style line 9 lt 5 lc rgb "blue" lw 2
set style line 10 lt 1 lc rgb "cyan" lw 2
set style line 11 lt 2 lc rgb "cyan" lw 2
set style line 12 lt 5 lc rgb "cyan" lw 2
set style line 13 lt 1 lc rgb "green" lw 2
set style line 14 lt 2 lc rgb "green" lw 2
set style line 15 lt 5 lc rgb "green" lw 2

print '>> doing velocity compare <<<<<<<<<<<<<<<<<';
## DEBYE AND STATS
lines=60;
lines1d=401;
dr_02d=0.5*L_db02d;
Omega_pe02d = 5.64e4 * sqrt(ne02d);
dt_02d = dt/Omega_pe02d;
length2d=sizez2d*0.5*L_db02d; 
length1d=nz1d*L_db01d*0.5;

## SPEED OF SOUND FOR SPECIES ++ 1D DVT's
## SI SPEEDS
mi_over_me = 1832*atom_mass - 1;
mn_over_me = 1832*atom_mass;
v_te = (dt/dr)*dr_02d/100./dt_02d;
v_ti = v_te/dt_ion;
v_tni = v_te/dt_nion;
v_tn = v_te/dt_ntrl;
## print "v_te = "; print v_te;
## print "v_tni = "; print v_tni;
## print "v_ti = "; print v_ti;
## print "v_tn = "; print v_tn; 

cs_e = (1/mi_over_me)**(1/2)*v_te;
cs_ntrl = dt_ntrl*cs_e;
cs_ion = dt_ion*cs_e;
cs_nion = dt_nion*cs_e;
print ">> cs_ntrl= "; print cs_ntrl;
print ">> cs_e = "; print cs_e;
print ">> cs_ion = "; print cs_ion;
print ">> cs_nion = "; print cs_nion;

## TICS AXIS OUTPUT
set output 'figs/vel1D-'.onedrunid.'.'.nstep1d.'_2D-'.twodrunid.'.'.time2d.'.png'
set multiplot layout 2,1
set xlabel 'z [cm]'
set ylabel 'velocity'

set xrange [0.0:length1d]
## set logscale y
## 1D ##########################
set title '1D velocities, time '.nstep1d
p onedfolder.'vfex'.time1d.'.dat' using ($0*L_db01d):(sum[col=1:lines1d] column(col)/(lines1d)) with lines ls 1 title 'el-v x' ,\
  onedfolder.'vfey'.time1d.'.dat' using ($0*L_db01d):(sum[col=1:lines1d] column(col)/(lines1d)) with lines ls 2 title 'el-v y' ,\
  onedfolder.'vfez'.time1d.'.dat' using ($0*L_db01d):(sum[col=1:lines1d] column(col)/(lines1d)) with lines ls 3 title 'el-v z' ,\
  onedfolder.'vfOmx'.time1d.'.dat' using ($0*L_db01d):(sum[col=1:lines1d] column(col)/(lines1d)) with lines ls 4 title 'Om-v x' ,\
  onedfolder.'vfOmy'.time1d.'.dat' using ($0*L_db01d):(sum[col=1:lines1d] column(col)/(lines1d)) with lines ls 5 title 'Om-v y' ,\
  onedfolder.'vfOmz'.time1d.'.dat' using ($0*L_db01d):(sum[col=1:lines1d] column(col)/(lines1d)) with lines ls 6 title 'Om-v z' ,\
  onedfolder.'vfO2px'.time1d.'.dat' using ($0*L_db01d):(sum[col=1:lines1d] column(col)/(lines1d)) with lines ls 7 title 'O2p-v x' ,\
  onedfolder.'vfO2py'.time1d.'.dat' using ($0*L_db01d):(sum[col=1:lines1d] column(col)/(lines1d)) with lines ls 8 title 'O2p-v y' ,\
  onedfolder.'vfO2pz'.time1d.'.dat' using ($0*L_db01d):(sum[col=1:lines1d] column(col)/(lines1d)) with lines ls 9 title 'O2p-v z'

set xrange [0.0:length2d]
unset logscale y
## 2D ##########################
set title '2D velocities, time '.time2d
p 'transpose2develr.dat' using ($0*dr_02d):(sum[col=1:lines] column(col)*cs_e/(lines)) with lines ls 1 title 'e-vel r' ,\
  'transpose2develz.dat' using ($0*dr_02d):(sum[col=1:lines] column(col)*cs_e/(lines)) with lines ls 2 title 'e-vel z' ,\
	'transpose2divelr.dat' using ($0*dr_02d):(sum[col=1:lines] column(col)*cs_ion/(lines)) with lines ls 4 title 'i-vel r' ,\
  'transpose2divelz.dat' using ($0*dr_02d):(sum[col=1:lines] column(col)*cs_ion/(lines)) with lines ls 5 title 'i-vel z' ,\
	'transpose2dnivelr.dat' using ($0*dr_02d):(sum[col=1:lines] column(col)*cs_nion/(lines)) with lines ls 7 title 'ni-vel r' ,\
  'transpose2dnivelz.dat' using ($0*dr_02d):(sum[col=1:lines] column(col)*cs_nion/(lines)) with lines ls 8 title 'ni-vel z' ,\
	'transpose2dnvelr.dat' using ($0*dr_02d):(sum[col=1:lines] column(col)*cs_ntrl/(lines)) with lines ls 10 title 'n-vel r' ,\
  'transpose2dnvelz.dat' using ($0*dr_02d):(sum[col=1:lines] column(col)*cs_ntrl/(lines)) with lines ls 11 title 'n-vel z'
  ##'transpose2dnvelt.dat' using ($0*dr_02d):(sum[col=1:lines] column(col)*cs_ntrl/(lines)) with lines ls 12 title 'n-vel t'
  ##'transpose2dnivelt.dat' using ($0*dr_02d):(sum[col=1:lines] column(col)*cs_nion/(lines)) with lines ls 9 title 'ni-vel t'
  ##'transpose2divelt.dat' using ($0*dr_02d):(sum[col=1:lines] column(col)*cs_ion/(lines)) with lines ls 6 title 'i-vel t' 
  ##'transpose2develt.dat' using ($0*dr_02d):(sum[col=1:lines] column(col)*cs_e/(lines)) with lines ls 3 title 'e-vel t'

unset multiplot
## DONE ##########################

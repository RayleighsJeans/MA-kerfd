## input parameters
lines=50;
set terminal gif animate delay 10 enhanced size 3072,1024
set grid; unset key; set border linewidth 2.0; set tics nomirror out scale 0.7;

load 'jet.pal'

set xlabel 'z (cell)'; set ylabel 'r (cell)'; 
set logscale cb; set format cb "10^{%T}";
set xrange [0.0:sizez2d]; set yrange [0.0:nr]; set cbrange [1e-15:10.0];

print ">> STARTING 2D DENSITIES GIF"
set output 'figs/densities2D-'.twodrunid.'.gif';

do for [timecode in timevec2d] {
	set multiplot layout 1,3
	set title 'edens '.timecode
	plot '../out/e_dens_'.timecode.'.dat' matrix with image
	set title 'idens '.timecode
	plot '../out/i_dens_'.timecode.'.dat' matrix with image
	set title 'nidens '.timecode
	plot '../out/ni_dens_'.timecode.'.dat' matrix with image
	unset multiplot
}

reset;
# 1D GIF
set terminal gif animate delay 10 enhanced size 1024,720
set grid; set border linewidth 2.0; set tics nomirror out scale 0.7;
set key outside;
sizez1d=nz1d*L_db01d/2;
set xrange [0.0:sizez1d]; set yrange [0.0:0.2*ne01d];

set xlabel 'z/cm'; set ylabel 'n/cm^{-3}';

print ">> STARTING 1D DENSITIES GIF"
set output 'figs/densities1D-'.onedrunid.'.gif'

do for [timecode in timevec1d] {
	set title 'densities at '.timecode
	plot onedfolder.'ne'.timecode.'.dat' u ($1*L_db01d):($2*ne01d) w l linestyle 1 title 'electrons' ,\
			 onedfolder.'O2p'.timecode.'.dat' u ($1*L_db01d):($2*ne01d) w l linestyle 4 title 'O2+' ,\
			 onedfolder.'Omin'.timecode.'.dat'  u ($1*L_db01d):($2*ne01d) w l linestyle 7 title 'O-'
}

reset;
# VELOCITIES 1D GIF
set terminal gif animate delay 10 enhanced size 1024,720
set grid; set border linewidth 2.0; set tics nomirror out scale 0.7;
sizez1d=nz1d*L_db01d/2;
set xrange [0.0:sizez1d]; set yrange [-200.0:200.0];

set xlabel 'z/cm'; set ylabel 'velocities/(cell p. step)';

print ">> STARTING 1D VELOCITIES GIF"
set output 'figs/velocities1D-'.onedrunid.'.gif'

do for [timecode in timevec1d] {
	set title '1d velocities at '.timecode
	plot onedfolder.'uez'.timecode.'.dat' u ($1*L_db01d):($2) w l linestyle 1 title 'v el z' ,\
			 'transpose/uer'.timecode.'.dat' u ($1*L_db01d):($2) w l linestyle 2 title 'v el r' ,\
			 onedfolder.'uOmz'.timecode.'.dat'  u ($1*L_db01d):($2) w l linestyle 4 title 'v O- z' ,\
			 'transpose/uOmr'.timecode.'.dat'  u ($1*L_db01d):($2) w l linestyle 5 title 'v O- r ' ,\
			 onedfolder.'uO2pz'.timecode.'.dat'  u ($1*L_db01d):($2) w l linestyle 7 title 'v O2+ z' ,\
			 'transpose/uO2pr'.timecode.'.dat'  u ($1*L_db01d):($2) w l linestyle 8 title 'v O2+ r'
}

reset;
# VELOCITIES 2D GIF
set terminal gif animate delay 10 enhanced size 1024,720
set grid; set border linewidth 2.0; set tics nomirror out scale 0.7;
set xrange [0.0:sizez2d*L_db02d/2]; set yrange[-200.0:200.0];

set xlabel 'z/cm'; set ylabel 'velocities/(cell p. step)';

print ">> STARTING 2D VELOCITIES GIF"
set output 'figs/velocities2D-'.twodrunid.'.gif'

do for [timecode in timevec2d] {
	set title '2d velocities at '.timecode
	plot 'transpose/trans-e_velr_'.timecode.'.dat' u ($0*L_db02d/2):(sum[col=1:lines] column(col)/(lines)) w l linestyle 1 title 'v el r' ,\
			 'transpose/trans-e_velz_'.timecode.'.dat' u ($0*L_db02d/2):(sum[col=1:lines] column(col)/(lines)) w l linestyle 2 title 'v el z' ,\
			 'transpose/trans-i_velr_'.timecode.'.dat' u ($0*L_db02d/2):(sum[col=1:lines] column(col)/(lines)) w l linestyle 4 title 'v i r' ,\
			 'transpose/trans-i_velz_'.timecode.'.dat' u ($0*L_db02d/2):(sum[col=1:lines] column(col)/(lines)) w l linestyle 5 title 'v i z' ,\
			 'transpose/trans-ni_velr_'.timecode.'.dat' u ($0*L_db02d/2):(sum[col=1:lines] column(col)/(lines)) w l linestyle 7 title 'v ni r' ,\
			 'transpose/trans-ni_velz_'.timecode.'.dat' u ($0*L_db02d/2):(sum[col=1:lines] column(col)/(lines)) w l linestyle 8 title 'v ni z' ,\
			 'transpose/trans-n_velr_'.timecode.'.dat' u ($0*L_db02d/2):(sum[col=1:lines] column(col)/(lines)) w l linestyle 10 title 'v n r' ,\
			 'transpose/trans-n_velz_'.timecode.'.dat' u ($0*L_db02d/2):(sum[col=1:lines] column(col)/(lines)) w l linestyle 11 title 'v n z'
}

unset output
print ">> DONE GIF'ING"

## GRAVEYARD
# frame=0;
# if (timesize2d > 150) {
# 	id=1;
# 	set output 'figs/densities2D-'.twodrunid.'-p'.id.'.gif';
# }
#	frame=frame+1; if (frame == 151) { id=id+1; frame=0; print 'next round with id='.id;
#																 	 	 set output 'figs/densities2D-'.twodrunid.'-p'.id.'.gif'; }
